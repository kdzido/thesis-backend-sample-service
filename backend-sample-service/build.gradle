buildscript {
    ext {
        springBootVersion = '1.5.10.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath('io.spring.gradle:dependency-management-plugin:0.5.4.RELEASE')
        classpath 'com.bmuschko:gradle-docker-plugin:3.2.4'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.bmuschko.docker-remote-api'

import org.apache.tools.ant.filters.*
import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage

sourceCompatibility = 1.8
targetCompatibility = 1.8

group 'pl.pja.13868.news'
version '1.2.1'

repositories {
    mavenLocal()
    // TODO own repo (cloud | private)
    mavenCentral()
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integTest/java')
            srcDir file('src/integTest/groovy')
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Edgware.SR2"
    }
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-actuator')

    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.cloud:spring-cloud-config-client')
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
//    compile('org.springframework.cloud:spring-cloud-starter-hystrix')
    compile('org.springframework.security:spring-security-rsa')

    testCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    testCompile "org.codehaus.groovy.modules.http-builder:http-builder:${httpBuilderVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.spockframework:spock-core:${spockVersion}"
    testCompile("cglib:cglib-nodep:${cglibVersion}")

    testCompile('org.springframework.boot:spring-boot-starter-test')

    integTestCompile sourceSets.main.output
    integTestCompile "cglib:cglib-nodep:${cglibVersion}"
    integTestCompile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    integTestCompile "org.spockframework:spock-core:${spockVersion}"
    integTestCompile "org.awaitility:awaitility-groovy:${awaitilityVersion}"
}

task integTest(type: Test) {
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath
    description ='Integration tests'
    group = 'verification'
}

// TODO uncomment?
//integTest.mustRunAfter test

jar {
    baseName = 'sample-service'
}

task copyJars(dependsOn: jar, type: Copy) {
    from jar
    into "$buildDir/dockerfile"
}

task copyDockerFiles(dependsOn: [copyJars], type: Copy) {
    from 'src/docker'
    into "$buildDir/dockerfile"

    String springBootJar = "sample-service-${project.version}.jar"
    filter(ReplaceTokens, tokens: [springBootJar: springBootJar])
}

task buildDockerImage(type: DockerBuildImage) {
    dependsOn copyDockerFiles
    inputDir = project.file("$buildDir/dockerfile/Dockerfile").parentFile
    // TODO include build-info.properties

    String dockerTag = System.env.PIPELINE_BUILD_ID ?: "snapshot"
    tag = "qu4rk/thesis-sampleservice:${dockerTag}"
}

// TODO publish to artifactory?


